plugins {
    id "com.gradle.plugin-publish" version "0.11.0"
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id 'java'
    id 'idea'
}

sourceCompatibility = 11
targetCompatibility = 11

allprojects {
    group = "edu.wpi.first"
    version = "2022.0.0"

    if (project.hasProperty('publishVersion')) {
        version = project.publishVersion
    }
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    api project(':ToolchainPlugin')
    implementation 'org.ajoberstar.grgit:grgit-core:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation gradleTestKit()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

pluginBundle {
    website = 'https://github.com/wpilibsuite/native-utils'
    vcsUrl = 'https://github.com/wpilibsuite/native-utils'
    description = 'This plugin provides native build utilities for FRC projects.'
    tags = ['groovy', 'native', 'utils', 'maven', 'frc', 'wpilib']
}

gradlePlugin {
    plugins {
        NativeUtils {
            id = 'edu.wpi.first.NativeUtils'
            displayName = 'NativeUtils'
            implementationClass = 'edu.wpi.first.nativeutils.NativeUtils'
        }
    }
}

wrapper {
    gradleVersion = '6.8.1'
}
